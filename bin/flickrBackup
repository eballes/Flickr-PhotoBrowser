#!/usr/bin/perl 

use strict;
use warnings;

use autodie;
use Carp;
use Data::Dumper;
use Getopt::Long;
use File::Basename;
use File::Path qw(remove_tree);
use LWP::Simple;
use Image::Size;

use Flickr::PhotoBrowser;

# Key:
#  e5df46b2cf8cfe59f58df245ec09c9d4
#
# Secret:
#  e1b9e34909d1c9f2

sub logApp {
    my ($pb) = @_;
    my $token;

    # Get token from file
    if ( -e $ENV{HOME} . "/.FlickrBackup" ) {
        open my $fd, '<', $ENV{HOME} . "/.FlickrBackup";
        $token = scalar <$fd>;
        close $fd;

        $token = $pb->checkToken($token);
    }

    print "Token: $token\n";
    my $loginResult = $pb->login( { token => $token } );

    if ( $loginResult->{token} ) {
        $token = $loginResult->{token};
    }
    elsif ( $loginResult->{uri} && $loginResult->{frob} ) {
        chomp( my $browser = `which firefox` || `which google-chrome` );
        system( $browser, $loginResult->{uri} ) if $browser;
        print "Go to\n-> $loginResult->{uri}\n";
        print "and grant read access to FlickrBackup\n";
        print "Press enter to continue when finished.\n";
        getc;

        $token = $pb->login( { frob => $loginResult->{frob} } )->{token};
    }

    open my $fd, '>', $ENV{HOME} . '/.FlickrBackup';
    print $fd $token;
    close $fd;

    return;
}

sub url2file {
    my ( $fpb, $flickrPhoto, $path, $size ) = @_;
    my ($extension) = $flickrPhoto->{url} =~ /\.(.{3,4}$)/;

    my ( $h, $w, $err ) = imgsize("$path/$flickrPhoto->{title}.$extension");
    if (   !-e "$path/$flickrPhoto->{title}.$extension"
        || !$fpb->isTheWantedSize( [ $h, $w ], $size ) )
    {
        LWP::Simple::getstore( $flickrPhoto->{url},
            "$path/$flickrPhoto->{title}.$extension" );
    } else { print "Skip download $flickrPhoto->{title}.$extension\n"}

    return;
}

sub safeTitle {
    my ($title) = @_;
    #print "Unsafe title: $title\n";

    ( my $safeTitle = $title ) =~ s/&amp;/&/g;
    $safeTitle =~ s{/}{-}g;

    #print "Safe title: $safeTitle\n"; 
    return $safeTitle;
}

sub backupSet {
    my ( $fpb, $basePath, $set, $size ) = @_;
    my $safeTitle = safeTitle($set->{title});

    my $path = "$basePath/$safeTitle";
    print "Going into... $path\n";
    mkdir "$path" unless -e $path;

    my @photos = @{ $fpb->getPicturesOfSet( $set->{id}, $size, 1, [] ) };
    foreach my $photo (@photos) {
        print "\tGetting photo... $photo->{title}\r";
        url2file( $fpb, $photo, $path, $size );
    }
    return;
}

sub backupCollection {
    my ( $fpb, $basePath, $collection, $size ) = @_;
    my $safeTitle = safeTitle($collection->{title});
    my $dirtyFlag = 0;

    my $path = "$basePath/$safeTitle";
    print "Going into... $path\n";
    mkdir "$path" unless -e $path;

    foreach my $element ( @{ $fpb->getCollections( $collection->{id} ) } ) {
        $dirtyFlag = 1;
        backupCollection( $fpb, $path, $element, $size );
    }

    return if $dirtyFlag;
    foreach my $set ( @{ $fpb->getSetsOfCollections( $collection->{id} ) } ) {
        backupSet( $fpb, $path, $set, $size );
    }

}

sub flickrBackup {
    my ( $path, $size, $options ) = @_;
    my $clean     = $options->{clean};
    my $albumName = $options->{name};

    remove_tree($path) && mkdir $path if -e $path && $clean;

    my $fpb = Flickr::PhotoBrowser->new(
        {
            key    => 'e5df46b2cf8cfe59f58df245ec09c9d4',
            secret => 'e1b9e34909d1c9f2',
        }
    );
    logApp($fpb);

    my ( $colId, $setId );
    if ($albumName) {
        $colId = $fpb->retrieveCollectionID($albumName);
        $setId = $fpb->retrieveSetID($albumName);

        print "Album Query: $albumName ColId: $colId  SetId: $setId\n";

        if ( !$colId and !$setId ) {
            croak "Nothing found :(";
        }
        if ( $colId and $setId ) {
            carp "Ambiguous name... matches with set and collection.\n"
              . "Retrieving collection by default";
            $setId = undef;
        }
    }

    if ( !( $colId || $setId ) ) {
        foreach my $collection ( @{ $fpb->getCollections() } ) {
            backupCollection( $fpb, $path, $collection, $size );
        }
    }
    else {
        if ($colId) {
            backupCollection( $fpb, $path,
                { id => $colId, title => $albumName }, $size );
        }
        else {
            backupSet( $fpb, $path, { id => $setId, title => $albumName },
                $size );
        }
    }
}

######################### MAIN ########################################
sub help {
    print "\n", basename($0), <<EOF;
 -p <output_path> [ --name <collection/set name>] [--clean] [--size <size>] [-h]

Clean flag removes all the content in <output_path> before starting to
download any photo. 

Possible size values: url_t url_s url_m url_o url_sq

EOF
    exit;
}

sub main {
    GetOptions(
        'path|p=s' => \( my $backupPath = undef ),
        'name|n=s' => \( my $albumName  = undef ),
        'size|s=s' => \( my $size       = 'url_t' ),
        'clean|c'  => \( my $cleanExec  = undef ),
        'help|h'   => \( my $printHelp  = undef ),
    );

    #url_sq, url_t, url_s, url_m, url_z, url_l, url_o

    help() if $printHelp;
    help() unless $backupPath;
    help() unless $size =~ /url_(:?[tsmozl]|sq)/;

    flickrBackup( $backupPath, $size,
        { name => $albumName, clean => $cleanExec } );
}

main();

__END__

=head1 NAME

flickrBackup - Download your photos from C<flickr.com>

=head1 SYNOPSIS

flickrBackup --path <output_path> [ --name <collection/set name>] 
    [--clean] [--size <size>] [-h]

=head1 DESCRIPTION

Batch image dowloader for the L<Flickr.com> service.

L<flickrBackup> makes easy to perform backups of sets and 
collections from Flickr.

=head1 OPTIONS

=over 4

=item --path

The C<--path> option indicates L<flickrBackup> where to store the
backup. This argument is mandatory.

=item --name

The C<--name> option asks L<flickrBackup> to do a partial backup of a 
particular collection or set. If a collection and a set share the same
name, the collection is dowloaded.

=item --size

The C<--size> option tells L<flickrBackup> which size of the photos is
preferred for the backup. Options are:
  url_sq  - Square size
  url_t   - Thumbnail size
  url_s   - Small size
  url_m   - Medium size
  url_z   - Medium 640
  url_l   - Large size
  url_o   - Original size

=item --clean

The C<--clean> flag indicates L<flickrBackup> to erase the output
path passed by the C<--path> option. If this flag is not activated,
a backup can be resumed.

Warn: Just be careful. "Resume" means that the program is not downloading
a file if it's already present e.g. no checks are made to consider if the
size of the file present locally is the requested one.

=item -h

The C<-h> flag prints the help

=back

=head1 CONFIGURATION

A configuration file to store the application token is created the
first time the application is executed. 

  ~/.FlickrBackup

=head1 BUGS

Error handling could be better.

Resume or incremental download can be considerably improved

=head1 AUTHOR

Eduardo Ballesteros, L<eballes@gmail.com>.

=head1 SEE ALSO

L<flickr.com>

L<Flickr::Upload>

L<Net::Flickr::Backup>

L<http://flickr.com/services/api/>

L<http://www.flickr.com/help/filters/>

=cut

